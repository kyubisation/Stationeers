alias filter d0
alias tank d1
alias canisterPump d3
alias canisterStorage d4
alias wasteTank d5

alias canisterId r0
alias targetPressure r1
alias canisterAvailable r2
alias canisterPressure r3
alias tankPressure r4
alias pumpSetting r5
alias pressureDiff r6

# Max pipe pressure is 60.795MPa
# Units are KPa
define MaxTankPressure 45000
define MinWastePressure 100
# Unit of Canister are Pascal
define MaxCanisterPressure 9500000
define MaxSmartCanPressure 19500000

define SmartCanisterId -668314371

start:

# Activate filter if below MaxTankPressure
# Deactivate filter if above MaxTankPressure
# or below MinWastePressure
l tankPressure tank Pressure
ble MaxTankPressure tankPressure disableTank
l tankPressure wasteTank Pressure
ble tankPressure MinWastePressure disableTank
s filter On 1
j postTank
disableTank:
s filter On 0
postTank:

# Fill canister, if available
bdns canisterStorage done
ls canisterAvailable canisterStorage 0 Occupied
beqz canisterAvailable disableCanister
ls canisterId canisterStorage 0 OccupantHash
beq canisterId SmartCanisterId smartCanPressure
move targetPressure MaxCanisterPressure
j checkCanisterPressure

smartCanPressure:
move targetPressure MaxSmartCanPressure

checkCanisterPressure:
ls canisterPressure canisterStorage 0 Pressure
ble targetPressure canisterPressure disableCanister
sub pressureDiff targetPressure canisterPressure
div pumpSetting 5 targetPressure
mul pumpSetting pumpSetting pressureDiff
min pumpSetting pumpSetting 1
s canisterPump Setting pumpSetting
s canisterPump On 1
j done

disableCanister:
s canisterPump Setting 0
s canisterPump On 0

done:
yield
j start