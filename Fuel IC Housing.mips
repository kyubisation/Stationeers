alias mixer d0
alias tank d1
alias canisterPump d3
alias canisterStorage d4
alias h2Tank d5

alias canisterId r0
alias targetPressure r0
alias canisterAvailable r2
alias canisterPressure r3
alias tankPressure r4
alias h2TankPressure r5

# Max pipe pressure is 60.795MPa
# Units are KPa
define MaxTankPressure 45000
define MinWastePressure 100
# Unit of Canister are Pascal
define MaxCanisterPressure 9500000
define MaxSmartCanPressure 19500000

define SmartCanisterId -668314371

start:

# Activate mixer if below MaxTankPressure
# Deactivate mixer if above MaxTankPressure
# or above H2 tank pressure
l tankPressure tank Pressure
ble MaxTankPressure tankPressure disableTank
l h2TankPressure h2Tank Pressure
ble h2TankPressure tankPressure disableTank
s mixer On 1
j postTank
disableTank:
s mixer On 0
postTank:

# Fill canister, if available
bdns canisterStorage done
ls canisterAvailable canisterStorage 0 Occupied
beqz canisterAvailable disableCanister
ls canisterId canisterStorage 0 OccupantHash
beq canisterId SmartCanisterId smartCanPressure
move targetPressure MaxCanisterPressure
j checkCanisterPressure

smartCanPressure:
move targetPressure MaxSmartCanPressure

checkCanisterPressure:
ls canisterPressure canisterStorage 0 Pressure
ble targetPressure canisterPressure disableCanister
s canisterPump Setting 5
s canisterPump On 1
j done

disableCanister:
s canisterPump Setting 0
s canisterPump On 0

done:
yield
j start